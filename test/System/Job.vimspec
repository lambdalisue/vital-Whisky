Describe System.Job
  Before all
    let TIMEOUT = (!empty($CI)) ? 30000 : 5000

    let Path = vital#vital#import('System.Filepath')
    let Sandbox = vital#vital#import('System.Sandbox')

    let root = Path.abspath(Path.realpath('test/_testdata/System/Job'))

    function! Args(script, ...) abort closure
      let p = executable('python3') ? 'python3' : 'python'
      let s = Path.join(root, a:script)
      return [p, s] + a:000
    endfunction

    function! OnStderr(data) abort dict
      call map(a:data, { -> v:val[-1:] ==# "\r" ? v:val[:-2] : v:val })
      call map(a:data, { -> substitute(v:val, ''^\s\+\|\s\+$'', '''', ''g'') })
      call map(filter(a:data, { -> !empty(v:val)}), { -> themis#log(v:val) })
    endfunction

    function! OnReceiveText(buffer, data) abort
      if empty(a:buffer)
        call add(a:buffer, '')
      endif
      call map(a:data, { -> v:val[-1:] ==# "\r" ? v:val[:-2] : v:val })
      let a:buffer[-1] .= a:data[0]
      call extend(a:buffer, a:data[1:])
    endfunction
  End

  Before
    let Job = vital#vital#import('System.Job')
    let options = {
          \ 'on_stdout': { -> 0 },
          \ 'on_stderr': funcref('OnStderr'),
          \}
  End

  Describe .start({cmd}[, {options}])
    It starts a job with {cmd} (List) and return a job instance
      let job = Job.start(Args('exit.py'), options)
      Assert KeyExists(job, 'status')
      Assert KeyExists(job, 'wait')
      Assert KeyExists(job, 'send')
      Assert KeyExists(job, 'stop')
      call job.stop()
      call job.wait(TIMEOUT)
    End

    It throws an exception when {args} is not a List instance
      Throws /Argument requires to be a List instance/ Job.start('')
      Throws /Argument requires to be a List instance/ Job.start(0)
      Throws /Argument requires to be a List instance/ Job.start({})
    End

    It throws an exception when {args} is an empty list
      Throws /Argument vector must have at least one item/ Job.start([])
    End

    It throws an exception when a first item of {args} is not executable
      Throws /"foo" is not an executable/ Job.start(['foo'])
    End

    if has('patch-8.0.0902') || has('nvim')
      Context cwd option
        Before
          let sandbox = Sandbox.new()
        End

        After
          call sandbox.dispose()
        End

        It starts a job in a current working directory
          call sandbox.visit('foo')
          call sandbox.visit('bar')
          let rs = []
          let job = Job.start(Args('cwd.py'), extend(options, {
                \ 'on_stdout': funcref('OnReceiveText', [rs]),
                \}))
          Assert Equals(job.wait(TIMEOUT), 0)
          Assert Equals(rs, [sandbox.path('bar'), ''])
        End

        It starts a job in a given cwd
          call sandbox.visit('foo')
          call sandbox.visit('bar')
          let rs = []
          let job = Job.start(Args('cwd.py'), extend(options, {
                \ 'cwd': sandbox.path('foo'),
                \ 'on_stdout': funcref('OnReceiveText', [rs]),
                \}))
          Assert Equals(job.wait(TIMEOUT), 0)
          Assert Equals(rs, [sandbox.path('foo'), ''])
        End
      End
    endif

    if has('patch-8.0.0902') || has('nvim-0.5.0')
      Context env option
        It starts a job with env
          let rs = []
          let job = Job.start(Args('env.py', 'hello'), extend(options, {
                \ 'on_stdout': funcref('OnReceiveText', [rs]),
                \ 'env': {
                \   'hello': 'world',
                \ },
                \}))
          Assert Equals(job.wait(TIMEOUT), 0)
          Assert Equals(rs, ['world', ''])
        End
      End
    endif
  End

  Describe Callbacks
    Describe .on_stdout() callback
      It is called as a dictionary function of the job instance
        function! OnStdout(data) abort dict
          call add(self.results, a:data)
        endfunction

        let job = Job.start(Args('echo.py', '--out', 'stdout'), extend(options, {
              \ 'results': [],
              \ 'on_stdout': funcref('OnStdout'),
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(job.results, [
              \ ['Hello'],
              \ [' World'],
              \ ['', ''],
              \ ['Hello'],
              \ [' World'],
              \ ['', ''],
              \])
      End

      It is called with raw {data} (CR)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\r", '--out', 'stdout'), extend(options, {
              \ 'on_stdout': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ["\r"],
              \ ['Hello'],
              \ [' World'],
              \ ["\r"],
              \])
      End

      It is called with raw {data} (LF)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\n", '--out', 'stdout'), extend(options, {
              \ 'on_stdout': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ['', ''],
              \ ['Hello'],
              \ [' World'],
              \ ['', ''],
              \])
      End

      It is called with raw {data} (CRLF)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\r\n", '--out', 'stdout'), extend(options, {
              \ 'on_stdout': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ["\r", ''],
              \ ['Hello'],
              \ [' World'],
              \ ["\r", ''],
              \])
      End

      It is called with raw {data} without trailing newline (invalid POSIX text) (CR)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\r", '--out', 'stdout', '--without-trailing-newline'), extend(options, {
              \ 'on_stdout': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ["\r"],
              \ ['Hello'],
              \ [' World'],
              \])
      End

      It is called with raw {data} without trailing newline (invalid POSIX text) (LF)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\n", '--out', 'stdout', '--without-trailing-newline'), extend(options, {
              \ 'on_stdout': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ['', ''],
              \ ['Hello'],
              \ [' World'],
              \])
      End

      It is called with raw {data} without trailing newline (invalid POSIX text) (CRLF)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\r\n", '--out', 'stdout', '--without-trailing-newline'), extend(options, {
              \ 'on_stdout': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ["\r", ''],
              \ ['Hello'],
              \ [' World'],
              \])
      End
    End

    Describe .on_stderr() callback
      It is called as a dictionary function of the job instance
        function! OnStdout(data) abort dict
          call add(self.results, a:data)
        endfunction

        let job = Job.start(Args('echo.py', '--out', 'stderr'), extend(options, {
              \ 'results': [],
              \ 'on_stderr': funcref('OnStdout'),
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(job.results, [
              \ ['Hello'],
              \ [' World'],
              \ ['', ''],
              \ ['Hello'],
              \ [' World'],
              \ ['', ''],
              \])
      End

      It is called with raw {data} (CR)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\r", '--out', 'stderr'), extend(options, {
              \ 'on_stderr': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ["\r"],
              \ ['Hello'],
              \ [' World'],
              \ ["\r"],
              \])
      End

      It is called with raw {data} (LF)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\n", '--out', 'stderr'), extend(options, {
              \ 'on_stderr': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ['', ''],
              \ ['Hello'],
              \ [' World'],
              \ ['', ''],
              \])
      End

      It is called with raw {data} (CRLF)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\r\n", '--out', 'stderr'), extend(options, {
              \ 'on_stderr': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ["\r", ''],
              \ ['Hello'],
              \ [' World'],
              \ ["\r", ''],
              \])
      End

      It is called with raw {data} without trailing newline (invalid POSIX text) (CR)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\r", '--out', 'stderr', '--without-trailing-newline'), extend(options, {
              \ 'on_stderr': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ["\r"],
              \ ['Hello'],
              \ [' World'],
              \])
      End

      It is called with raw {data} without trailing newline (invalid POSIX text) (LF)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\n", '--out', 'stderr', '--without-trailing-newline'), extend(options, {
              \ 'on_stderr': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ['', ''],
              \ ['Hello'],
              \ [' World'],
              \])
      End

      It is called with raw {data} without trailing newline (invalid POSIX text) (CRLF)
        let rs = []
        let job = Job.start(Args('echo.py', '--newline', "\r\n", '--out', 'stderr', '--without-trailing-newline'), extend(options, {
              \ 'on_stderr': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [
              \ ['Hello'],
              \ [' World'],
              \ ["\r", ''],
              \ ['Hello'],
              \ [' World'],
              \])
      End
    End

    Describe .on_exit() callback
      It is called as a dictionary function of the job instance
        function! OnExit(exitval) abort dict
          call add(self.results, a:exitval)
        endfunction

        let job = Job.start(Args('exit.py', '--exitval', '0'), extend(options, {
              \ 'results': [],
              \ 'on_exit': funcref('OnExit'),
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(job.results, [0])
      End

      It is called with an exitval
        let rs = []
        let path = Path.join(root, 'exitval.py')
        let job = Job.start(Args('exit.py', '--exitval', '0'), extend(options, {
              \ 'on_exit': { v -> add(rs, v) },
              \}))
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, [0])

        let job = Job.start(Args('exit.py', '--exitval', '1'), options)
        Assert Equals(job.wait(TIMEOUT), 1)
        Assert Equals(rs, [0, 1])

        let job = Job.start(Args('exit.py', '--exitval', '2'), options)
        Assert Equals(job.wait(TIMEOUT), 2)
        Assert Equals(rs, [0, 1, 2])
      End
    End
  End

  Describe Job instance
    Describe .pid()
      It returns a unique number for individual process
        let job1 = Job.start(Args('never.py'), options)
        let job2 = Job.start(Args('never.py'), options)
        Assert IsNumber(job1.pid())
        Assert IsNumber(job2.pid())
        Assert NotEqual(job1.pid(), job2.pid())
        call job1.stop()
        call job2.stop()
        call job1.wait(TIMEOUT)
        call job2.wait(TIMEOUT)
      End

      It keeps equal PID even when a process exit
        let job = Job.start(Args('never.py'), options)
        let pid = job.pid()
        call job.stop()
        call job.wait(TIMEOUT)
        Assert IsNumber(job.pid())
        Assert Equals(job.pid(), pid)
      End
    End

    Describe .status()
      It returns 'run' when a job is running
        let job = Job.start(Args('never.py'), options)
        Assert Equals(job.status(), 'run')
        call job.stop()
        call job.wait(TIMEOUT)
      End

      It returns 'dead' when a job has terminated
        let job = Job.start(Args('never.py'), options)
        call job.stop()
        call job.wait(TIMEOUT)
        Assert Equals(job.status(), 'dead')
      End
    End

    Describe .send()
      It sends {data} (String) to a job
        let rs = []
        let job = Job.start(Args('reply.py'), extend(options, {
              \ 'on_stdout': funcref('OnReceiveText', [rs]),
              \}))
        call job.send('Hello')
        call job.send(" World\n")
        call job.close()
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(rs, ['received: Hello World', ''])
      End

      It sends {data} (List) to the job
        let rs = []
        let job = Job.start(Args('reply.py'), extend(options, {
              \ 'on_stdout': funcref('OnReceiveText', [rs]),
              \}))
        call job.send(['abc', "123\n456", ''])
        call job.send(['.', ''])
        call job.close()
        Assert Equals(job.wait(TIMEOUT), 0)
        if has('nvim')
          Assert Equals(rs, [
                \ 'received: abc',
                \ 'received: 123<NUL>456',
                \ 'received: .',
                \ '',
                \])
        else
          " Vim cannot send \0
          Assert Equals(rs, [
                \ 'received: abc',
                \ 'received: 123456',
                \ 'received: .',
                \ '',
                \])
        endif
      End
    End

    Describe .close()
      It sends EOF (close stdin) to a job [time-consuming]
        let rs = []
        let job = Job.start(Args('reply.py', '--interval', '0'), extend(options, {
              \ 'on_stdout': funcref('OnReceiveText', [rs]),
              \}))
        call job.send("Hello\n")
        call job.send("Darkness\n")
        call job.send("My\n")
        call job.send("Old\n")
        call job.send("Friend\n")
        " EOF has not been sent so the following should be timeout
        Assert Equals(job.wait(TIMEOUT), -1)
        Assert Equals(rs, [
              \ 'received: Hello',
              \ 'received: Darkness',
              \ 'received: My',
              \ 'received: Old',
              \ 'received: Friend',
              \ '',
              \])
        call job.close()
        Assert Equals(job.wait(TIMEOUT), 0)
      End
    End

    Describe .stop()
      It stop a job which is running
        let job = Job.start(Args('never.py'), options)
        call job.stop()
        call job.wait(TIMEOUT)
        Assert Equals(job.status(), 'dead')
      End

      It does nothing when a job has already terminated
        let job = Job.start(Args('never.py'), options)
        call job.stop()
        call job.stop()
        call job.wait(TIMEOUT)
        Assert Equals(job.status(), 'dead')
      End
    End

    Describe .wait()
      It waits a job to terminate
        let job = Job.start(Args('echo.py'), options)
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(job.status(), 'dead')
      End

      It returns an exit status when a job has terminated
        let job = Job.start(Args('exit.py', '--delay', '100', '--exitval', '0'), options)
        Assert Equals(job.wait(TIMEOUT), 0)
        Assert Equals(job.status(), 'dead')

        let job = Job.start(Args('exit.py', '--delay', '100', '--exitval', '1'), options)
        Assert Equals(job.wait(TIMEOUT), 1)
        Assert Equals(job.status(), 'dead')

        let job = Job.start(Args('exit.py', '--delay', '100', '--exitval', '2'), options)
        Assert Equals(job.wait(TIMEOUT), 2)
        Assert Equals(job.status(), 'dead')
      End

      It returns an exit status when a job has already been terminated
        let job = Job.start(Args('exit.py', '--exitval', '0'), options)
        call job.wait(TIMEOUT)
        Assert Equals(job.status(), 'dead')
        Assert Equals(job.wait(TIMEOUT), 0)

        let job = Job.start(Args('exit.py', '--exitval', '1'), options)
        call job.wait(TIMEOUT)
        Assert Equals(job.status(), 'dead')
        Assert Equals(job.wait(TIMEOUT), 1)

        let job = Job.start(Args('exit.py', '--exitval', '2'), options)
        call job.wait(TIMEOUT)
        Assert Equals(job.status(), 'dead')
        Assert Equals(job.wait(TIMEOUT), 2)
      End

      It returns -1 when a job did not terminate within {timeout} [time-consuming]
        let job = Job.start(Args('never.py'), options)
        Assert Equals(job.wait(TIMEOUT), -1)
        Assert Equals(job.status(), 'run')
        call job.stop()
        call job.wait(TIMEOUT)
      End
    End
  End
End
